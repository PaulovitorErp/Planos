#INCLUDE "totvs.ch"
#INCLUDE "hbutton.ch"
#INCLUDE "TbiConn.ch"
#INCLUDE "topconn.ch"


//-------------------------------------------------------------------
/*/{Protheus.doc} USX6VIRTUS
Parametros das Rotinas
aRotUsu ( vetor com as rotinas que o usuario tera acesso ) 
Exemplo:    aRotUsu := {"001","004","005",...}
			aRotUsu := {"*"} AXTABELA (todos parametros)

@author  author
@since   date
@version version
@obs
Para inserir nova rotina, eh necessario incluir a mesma no
vetor "aRotTot", atribuir todos os parametros da rotina na
variavel "cParamet" e documentar nas "Rotinas disponiveis"

Rotinas disponiveis:
    001 - Contratos (RFUNA002)
    002 - Ativação de Contratos (RFUNA004)
    003 - Reajuste de Contratos (RFUNA010)
    004 - Cancelamento de Contratos (RFUNA015)
    005 - Geração do Pedido de Venda  (RFUNA017)
    006 - Títulos dos Contratos de Funerária  (RFUNA027)
    007 - Cobrança da Taxa de Manutenção  (RFUNA029)
    008 - Valores adicionais de Beneficiários  (RFUNA031)
    009 - Reajuste de Contrato  (RFUNA032)
    010 - Geração do Pedido de Venda  (RFUNA034)
    011 - Contrato de Convalescência (RFUNA044)
    012 - Geração dos Títulos do Contrato de Convalescência (RFUNA046)
    013 - Cancelamento em Lote (RFUNA051)
    014 - Impressão de Cartão (RFUNA052)

/*/
//-------------------------------------------------------------------
User Function USX6VIRTUS(aRotUsu)

	Local nI        := 0
	Local aMarcadas := {}
	Local lOpen   	:= .F.
	Local aRecnoSM0 := {}
	Local lShared   := .T. //Caso verdadeiro, indica que a tabela deve ser aberta em modo compartilhado, isto é, outros processos também poderão abrir esta tabela.

	Local aSay		:= {}
	Local aButton	:= {}
	Local lOk		:= .F.
	Local cTitulo	:= "ATUALIZAÇÃO DE PARÂMETROS - VIRTUS"
	Local cDesc1	:= "Esta rotina tem como função fazer a atualização dos parâmetros: SX6"
	Local cDesc2	:= ""
	Local cDesc3	:= ""
	Local cDesc4	:= ""
	Local cDesc5	:= ""
	//Local cDesc6	:= ""
	//Local cDesc7	:= ""

	Public oMainWnd := NIL

	#IFDEF TOP
		TCInternal( 5, "*OFF" ) // Desliga Refresh no Lock do Top
	#ENDIF

	__cInterNet := NIL
	__lPYME     := .F.

	Set Dele On //-- habilita filtro do campo DELET

// Mensagens de Tela Inicial
	aAdd( aSay, "ATUALIZAÇÃO DE DICIONÁRIOS E TABELAS - VIRTUS")
	aAdd( aSay, "" )
	aAdd( aSay, cDesc1 )
	aAdd( aSay, cDesc2 )
	aAdd( aSay, cDesc3 )
	aAdd( aSay, cDesc4 )
	aAdd( aSay, cDesc5 )
//aAdd( aSay, cDesc6 )
//aAdd( aSay, cDesc7 )

// Botoes Tela Inicial
	aAdd(  aButton, {  1, .T., { || lOk := .T., FechaBatch() } } )
	aAdd(  aButton, {  2, .T., { || lOk := .F., FechaBatch() } } )

	FormBatch(  cTitulo,  aSay,  aButton )

	If lOk

		aMarcadas := U_UPDESEMP(lShared)

		If !Empty( aMarcadas ) .and. ( lOpen := U_UPDOPSM0(lShared) ) //!Empty(cGetEmp) .and. !Empty(cGetFil)

			dbSelectArea( "SM0" )
			dbGoTop()

			While !SM0->( EOF() )
				// So adiciona no aRecnoSM0 se a empresa for diferente
				If aScan( aRecnoSM0, { |x| x[2] == SM0->M0_CODIGO } ) == 0 ;
						.AND. aScan( aMarcadas, { |x| x[1] == SM0->M0_CODIGO } ) > 0
					aAdd( aRecnoSM0, { Recno(), SM0->M0_CODIGO } )
				EndIf
				SM0->( dbSkip() )
			End

			SM0->( dbCloseArea() )

			If lOpen

				For nI := 1 To Len( aRecnoSM0 )

					If !( lOpen := U_UPDOPSM0(lShared) )
						Help(NIL, NIL, "ATENÇÃO", NIL, "Atualização da empresa " + aRecnoSM0[nI][2] + " não efetuada.", 1, 0, NIL, NIL, NIL, NIL, NIL, {""})
						Exit
					EndIf

					SM0->( dbGoTo( aRecnoSM0[nI][1] ) )

					//RpcSetType( 3 )
					//RpcSetEnv( SM0->M0_CODIGO, SM0->M0_CODFIL )

					cEmpAnt := AllTrim(SM0->M0_CODIGO)
					cFilAnt := AllTrim(SM0->M0_CODFIL)

					//-- Preparar ambiente local na retagauarda
					RpcSetType(3)
					PREPARE ENVIRONMENT EMPRESA cEmpAnt FILIAL cFilAnt MODULO "FRT"

					lMsFinalAuto := .F.
					lMsHelpAuto  := .F.

				DoTelaSX6(aRotUsu)

			Next nI

		EndIf
	EndIf

EndIf

Return()

//-------------------------------------------------------------------
/*/{Protheus.doc} DoTelaSX6
DoTelaSX6: mostra a tela de parametros
@author  author
@since   date
@version version
/*/
//-------------------------------------------------------------------
Static Function DoTelaSX6(aRotUsu)

	Local aObjects  := {} , aPos := {} , aInfo := {}
	Local aSizeHalf := MsAdvSize(.T.)  // Tamanho Maximo da Janela (.T.=TOOLBAR,.F.=SEM TOOLBAR)

	Local nI        := 0
	Local cRot      := ""

	Private overd   := LoadBitmap( GetResources(), "BR_VERDE")    // parametro existente na base
	Private overm   := LoadBitmap( GetResources(), "BR_VERMELHO") // parametro nao existente na base
	Private aVetPar    := {{"overm","","","",""}} // vetor dos parametros
	Private aRot    := {}   // Rotinas disponiveis
	Private aRotTot := {""} // Todas as Rotinas
	Private oParRot, oLbPar

	Default aRotUsu := {}   // Parametro - Rotinas que o usuario tem acesso

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Rotinas               ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
    aAdd(aRotTot,"001="+"Contratos (RFUNA002)")
    aAdd(aRotTot,"002="+"Ativação de Contratos (RFUNA004)")
    aAdd(aRotTot,"003="+"Reajuste de Contratos (RFUNA010)")
    aAdd(aRotTot,"004="+"Cancelamento de Contratos (RFUNA015)")
    aAdd(aRotTot,"005="+"Geração do Pedido de Venda  (RFUNA017)")
    aAdd(aRotTot,"006="+"Títulos dos Contratos de Funerária  (RFUNA027)")
    aAdd(aRotTot,"007="+"Cobrança da Taxa de Manutenção  (RFUNA029)")
    aAdd(aRotTot,"008="+"Valores adicionais de Beneficiários  (RFUNA031)")
    aAdd(aRotTot,"009="+"Reajuste de Contrato  (RFUNA032)")
    aAdd(aRotTot,"010="+"Geração do Pedido de Venda  (RFUNA034)")
    aAdd(aRotTot,"011="+"Contrato de Convalescência (RFUNA044)")
    aAdd(aRotTot,"012="+"Geração dos Títulos do Contrato de Convalescência (RFUNA046)")
    aAdd(aRotTot,"013="+"Cancelamento em Lote (RFUNA051)")
    aAdd(aRotTot,"014="+"Impressão de Cartão (RFUNA052)")

	If len(aRotUsu) == 0 // Nao foi passado nenhuma rotina como parametro
		aRot := aClone(aRotTot) // disponibilizar todas as rotinas
		cRot := SubStr(aRotTot[1],1,3)
	Else // Foi passada rotinas como parametro
		If len(aRotUsu) == 1 // Apenas uma rotina
			cRot := SubStr(aRotUsu[1],1,3)
			U_RUTIL68D(cRot,.f.) // Filtrar parametros da rotina
		Else // Varias rotinas
			aRot := {""}
		EndIf
		For ni := 1 to len(aRotTot)
			If Ascan(aRotUsu,SUBSTR(aRotTot[ni],1,3)) > 0 // verificar se usuario pode acessar a rotina
				aAdd(aRot,aRotTot[ni]) // adicionar a rotina
			EndIf
		Next
	EndIf

	If aSizeHalf[3] = 0
//aSizeHalf[1] := 0   //1 - Linha inicial
//aSizeHalf[2] := 0   //2 - Coluna Inicial
		aSizeHalf[3] := 970/2 //3 - Linha Final
		aSizeHalf[4] := 624/2 //4 - Coluna Final

		aSizeHalf[5] := 970 //5 - Separação X
		aSizeHalf[6] := 624 + 30 //6 - Separação Y
//aSizeHalf[7] //7 - Separação X da borda (Opcional)
//aSizeHalf[8] //7 - Separação X da borda (Opcional)
	EndIf

//Alert( "aSizeHalf: " + CRLF + U_XtoStrin(aSizeHalf) )

	aInfo := { aSizeHalf[ 1 ], aSizeHalf[ 2 ], aSizeHalf[ 3 ], aSizeHalf[ 4 ], 3, 3 } // Tamanho total da tela
	aObjects := {}
	AAdd( aObjects, { 0, 30, .T. , .F. } ) // EnchoiceBar
	AAdd( aObjects, { 0, 21, .T. , .F. } ) // Pesquisar
	AAdd( aObjects, { 0, 10, .T. , .T. } ) // ListBox
	aPos := MsObjSize( aInfo, aObjects )

	//
	DEFINE MSDIALOG oParRot TITLE ("Parâmetros da Rotina - Empresa : " + SM0->M0_CODIGO + "/" + AllTrim(SM0->M0_NOME)) FROM aSizeHalf[7],0 TO aSizeHalf[6],aSizeHalf[5] /*OF oMainWnd*/ PIXEL // Parametros da Rotina
	oParRot:lEscClose := .F.
	@ aPos[2,1]+00,aPos[2,2]+00 TO aPos[2,1]+21,85 LABEL "Parâmetro existente na Base?" OF oParRot PIXEL // Parametro existente na Base?
	@ aPos[2,1]+10,aPos[2,2]+14 BITMAP OXverd RESOURCE "BR_VERDE" OF oParRot NOBORDER SIZE 10,10 PIXEL
	@ aPos[2,1]+10,aPos[2,2]+24 SAY "Sim" SIZE 30,8 OF oParRot PIXEL COLOR CLR_BLUE // Sim
	@ aPos[2,1]+10,aPos[2,2]+47 BITMAP OXverm RESOURCE "BR_VERMELHO" OF oParRot NOBORDER SIZE 10,10 PIXEL
	@ aPos[2,1]+10,aPos[2,2]+57 SAY "Nao" SIZE 30,8 OF oParRot PIXEL COLOR CLR_BLUE // Nao
	@ aPos[2,1]+00,aPos[2,2]+85 TO aPos[2,1]+21,aPos[2,4] LABEL "Rotina" OF oParRot PIXEL // Rotina
	@ aPos[2,1]+07,aPos[2,2]+88 MSCOMBOBOX oRot VAR cRot SIZE aPos[2,4]-93,08 COLOR CLR_BLACK ITEMS aRot OF oParRot PIXEL ON CHANGE U_RUTIL68D(cRot,.t.) WHEN len(aRot) > 1
	@ aPos[3,1],aPos[3,2] LISTBOX oLbPar FIELDS HEADER "","Parâmetro","Tipo","Conteúdo","Descrição" COLSIZES 10,25,20,60,120 SIZE aPos[3,4]-04,aPos[3,3]-45 OF oParRot PIXEL ON DBLCLICK U_RUTIL68A(oLbPar:nAt)
	oLbPar:bHeaderClick := {|oObj,nCol| U_RUTIL68C(nCol) , } // Ordenar Parametros
	oLbPar:SetArray(aVetPar)
	oLbPar:bLine := { || { &(aVetPar[oLbPar:nAt,1]) , aVetPar[oLbPar:nAt,2] , aVetPar[oLbPar:nAt,3] , aVetPar[oLbPar:nAt,4] , aVetPar[oLbPar:nAt,5] }}
	ACTIVATE MSDIALOG oParRot CENTER ON INIT EnchoiceBar(oParRot,{|| oParRot:End() },{ || oParRot:End()},,)

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} RUTIL68A
Altera conteúdo do Parametro 
@author  author
@since   date
@version version
/*/
//-------------------------------------------------------------------
User Function RUTIL68A(nLinha)
	Local aRet      := {}
	Local aParamBox := {}
	Local cMascara  := ""
	Local xConteud  := ""
	Local aPars 	:= {}
	Local lExiste	:= (aVetPar[nLinha,1] == "overd") // Parametro existente na Base

	Local cAliasSX6 := GetNextAlias() // apelido para o arquivo de trabalho
	Local lOpen   	:= .F. // valida se foi aberto a tabela

	// abre o dicionário SX6
	OpenSXs(NIL, NIL, NIL, NIL, cEmpAnt, cAliasSX6, "SX6", NIL, .F.)
	lOpen := Select(cAliasSX6) > 0

	// caso aberto, posiciona no topo
	If !(lOpen)
		Return .F.
	EndIf

	xConteud := PADR(aVetPar[nLinha,4],250) //tamanho do X6_CONTEUD

	Do Case
	Case aVetPar[nLinha,3] == "C" // Caracter
		cMascara := ""
		xConteud := left(xConteud+space(300),len((cAliasSX6)->&("X6_CONTEUD")))
	Case aVetPar[nLinha,3] == "N" // Numerico
		cMascara := "@E 9999999999999999999999999"
	Case aVetPar[nLinha,3] == "D" // Data
		cMascara := "@D"
		xConteud := left(xConteud+space(10),len((cAliasSX6)->&("X6_CONTEUD")))
	Case aVetPar[nLinha,3] == "L" // Logico
		cMascara := "@!"
		xConteud := left(xConteud+space(3),3)
	Otherwise
		aVetPar[nLinha,3] := " "
	EndCase

	AADD(aParamBox,{ 1,"Parâmetro",aVetPar[nLinha,2],"@!","","",".F.",50,.t.}) // Parametro
	AADD(aParamBox,{ 1,"Tipo",aVetPar[nLinha,3],"@!","","",iif(lExiste,".F.",".T."),20,.t.}) // Tipo
	AADD(aParamBox,{ 1,"Conteúdo",xConteud,cMascara,"","","",120,.f.}) // Conteúdo
	AADD(aParamBox,{11,"Descrição",aVetPar[nLinha,5],"",iif(lExiste,".F.",".T."),.t.}) // Descrição

	If ParamBox(aParamBox,"Parâmetro",@aRet,,,,,,,,.f.) // Parametro
		If lExiste
			aVetPar[nLinha,4] := aRet[03] // Atualizar vetor no listbox conteúdo
			PutMv(aVetPar[nLinha,2],aRet[03]) // Gravar conteúdo no parametro
		Else
			aVetPar[nLinha,1] := "overd" // Atualiza vertor no listbox legenda
			aVetPar[nLinha,3] := aRet[02] // Atualizar vetor no listbox tipo
			aVetPar[nLinha,4] := aRet[03] // Atualizar vetor no listbox conteúdo
			aVetPar[nLinha,5] := aRet[04] // Atualizar vetor no listbox descrição
			aAdd(aPars, {aVetPar[nLinha,2], aRet[02], aRet[04], PADR(aRet[03],250)} )
			U_RUTIL68B(aPars)
		EndIf
	EndIf

Return()

/*/{Protheus.doc} U_RUTIL68B
Função para criação de parâmetros (SX6)
@type function
@author Totvs GO
@since 21/08/2019
@version 1.0
    @param aPars, Array, Array com os parâmetros do sistema
    @example
    U_RUTIL68B(aParametros)
    
    @obs Abaixo a estrutura do array:
        [01] - Parâmetro (ex.: "MV_X_TST")
        [02] - Tipo (ex.: "C")
        [03] - Descrição (ex.: "Parâmetro Teste")
        [04] - Conteúdo (ex.: "123;456;789")
/*/

User Function RUTIL68B(aPars)
	Local nAtual	:= 0
	Local aArea		:= GetArea()
	//Local aAreaX6	:= SX6->(GetArea())
	
	Local cAliasSX6 := GetNextAlias() // apelido para o arquivo de trabalho
	Local lOpen   	:= .F. // valida se foi aberto a tabela
	Default aPars	:= {}

	// abre o dicionário SX6
	OpenSXs(NIL, NIL, NIL, NIL, cEmpAnt, cAliasSX6, "SX6", NIL, .F.)
	lOpen := Select(cAliasSX6) > 0

	// caso aberto, posiciona no topo
	If !(lOpen)
		Return .F.
	EndIf
	DbSelectArea(cAliasSX6)
	(cAliasSX6)->( DbSetOrder( 1 ) ) //X6_FIL+X6_VAR
	(cAliasSX6)->( DbGoTop() )

	//Percorrendo os parâmetros e gerando os registros
	For nAtual := 1 To Len(aPars)
		//Se não conseguir posicionar no parâmetro cria
		If !(cAliasSX6)->( dbSeek(xFilial()+aPars[nAtual][1]) )
			RecLock( cAliasSX6, .T. )
			//Geral
			(cAliasSX6)->&("X6_VAR")     := aPars[nAtual][1]
			(cAliasSX6)->&("X6_TIPO")    := aPars[nAtual][2]
			(cAliasSX6)->&("X6_PROPRI")  := "U"
			//Descrição
			(cAliasSX6)->&("X6_DESCRIC") := SubStr(aPars[nAtual][3],1,50)
			(cAliasSX6)->&("X6_DESC1")   := SubStr(aPars[nAtual][3],51,100)
			(cAliasSX6)->&("X6_DESC2")   := SubStr(aPars[nAtual][3],101,150)
			//Conteúdo
			(cAliasSX6)->&("X6_CONTEUD") := aPars[nAtual][4]
			SX6->(MsUnlock())
		EndIf
	Next
	
	DbCommitAll()
	DbUnlockAll()
	// FECHA O ARQUIVO DE TRABALHO
    DbCloseArea()

	//RestArea(aAreaX6)
	RestArea(aArea)

Return

//-------------------------------------------------------------------
/*/{Protheus.doc} RUTIL68C
Ordenar listbox - vetor dos Parametros
@author  author
@since   date
@version version
/*/
//-------------------------------------------------------------------
User Function RUTIL68C(nCol)
	If nCol > 1
		Asort(aVetPar,,,{|x,y| x[nCol] < y[nCol] })
		oLbPar:Refresh()
		oLbPar:SetFocus()
	EndIf
Return()

//-------------------------------------------------------------------
/*/{Protheus.doc} RUTIL68D
Carrega os Parametros de uma determinada rotina
@author  author
@since   date
@version version
/*/
//-------------------------------------------------------------------
User Function RUTIL68D(cRot,lRefr)
	Local ni       := 0
	Local cParamet := "" // Parametro(10 posicoes) + / ...
	Local aParamet := {}
	Local aListPar := {} //nome, tipo, conteúdo, descrição (150 caracteres)

	Local cAliasSX6 := GetNextAlias() // apelido para o arquivo de trabalho
	Local lOpen   	:= .F. // valida se foi aberto a tabela
	Local bGetMvFil	:= {|cParametro,lHelp,cDefault,cFil| GetMV(cParametro,lHelp,cDefault,cFil) }

	// abre o dicionário SX6
	OpenSXs(NIL, NIL, NIL, NIL, cEmpAnt, cAliasSX6, "SX6", NIL, .F.)
	lOpen := Select(cAliasSX6) > 0

	// caso aberto, posiciona no topo
	If !(lOpen)
		Return .F.
	EndIf
	
	aVetPar := {} //zero o array do LISTBOX

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Parametros por Rotina ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Do Case

	Case cRot == "*" // Todos os parametros da BASE
		DbSelectArea(cAliasSX6)
		(cAliasSX6)->( DbSetOrder( 1 ) ) //X6_FIL+X6_VAR
		(cAliasSX6)->( DbGoTop() )
		While !Eof()
			aAdd(aVetPar,{ "overd" , (cAliasSX6)->&("X6_VAR") , (cAliasSX6)->&("X6_TIPO") , Alltrim(X6Conteud()) , Alltrim(X6Descric())+" "+Alltrim(X6Desc1())+" "+Alltrim(X6Desc2()) })
			dbskip()
		Enddo

	Case cRot == "001" // VIRTUS - Totvs PDV

		// Lista dos parametros encontrados no projeto tpdvposto
		cParamet += "ES_AJUS054/ES_ALCADA /ES_DBTSS  /ES_IPTSS  /ES_NWLTTSS/ES_PORTTSS/ESP_MOTOR /ESP_TRANSP/ESP_VEICUL/ESP_XAGLUT/" 
		cParamet += "MV_LJPOSTO/MRPC_CAIXA/MV_1DUP   /MV_ARREFAT/MV_CFOPREM/MV_CFOPTRA/MV_CLIPAD /MV_CODREG /MV_COMBUS /MV_DATAFIN/" 
		cParamet += "MV_DATAREC/MV_DEVNCC /MV_ENVCDGE/MV_ESPECIE/MV_ESTADO /MV_FTTEFGU/MV_FTTEFLI/MV_ICSTDEV/MV_IPIBENE/MV_IPIDEV /" 
		cParamet += "MV_LIBCHEQ/MV_LJAMBIE/MV_LJCHGDV/MV_LJCMPCR/MV_LJCMPNC/MV_LJCNVDA/MV_LJCNVDA/MV_LJCOLOR/MV_LJCONDE/MV_LJCPNCC/" 
		cParamet += "MV_LJDCLEG/MV_LJDCLEI/MV_LJFCVDA/MV_LJINTER/MV_LJINTER/MV_LJMULTA/MV_LJSTPRT/MV_LJTPCOM/MV_LJTPDES/MV_LJTRDIN/" 
		cParamet += "MV_LJTROCO/MV_LJTXNFE/MV_LJVLNCC/MV_LOJAPAD/MV_MENBOL1/MV_MENBOL2/MV_MENBOL3/MV_MODALID/MV_MODNFCE/MV_MOEDA1 /" 
		cParamet += "MV_NFCEDES/MV_NFCEEXC/MV_NFCEIMP/MV_NFCEURL/MV_NFCEUTC/MV_NUMFAT /MV_RELACNT/MV_RELAUTH/MV_RELPSW /MV_MOEDAP1/" 
		cParamet += "MV_RELSERV/MV_SIMB1  /MV_SPEDEXC/MV_SPEDURL/MV_TABPAD /MV_TESSAI /MV_TXPER  /MV_USACRED/MV_VENDPAD/MV_VERNFCE/" 
		cParamet += "MV_XALTSAC/MV_XATAJAB/MV_XATUPRC/MV_XBCOCDV/MV_XBOMBAR/MV_XCALVEN/MV_XCCDVSP/MV_XCCPDV /MV_XCODBAR/MV_XCOMBUS/" 
		cParamet += "MV_XDIASVC/MV_XDIASVS/MV_XDIFMAX/MV_XDIRBMO/MV_XDIRBOL/MV_XDIRDAN/MV_XDIRFAT/MV_XDIRSER/MV_XDIRSYS/MV_XDIVERG/" 
		cParamet += "MV_XENVARQ/MV_XFCHHMI/TP_FPGCONV/MV_XFPGCLI/MV_XFPGFAT/MV_XFUNSLI/MV_XGERANF/MV_XGRARLA/MV_XIMPEXT/MV_XIMPPAD/" 
		cParamet += "MV_XINCLEJ/MV_XINCSLI/MV_XLMCORG/MV_XLOCREC/MV_XMAILCC/MV_XMAILEX/MV_XMAILFT/MV_XMAILIN/MV_XMARAJO/MV_XMAXDIV/" 
		cParamet += "MV_XMAXFUS/MV_XMRGVAL/MV_XMSGADI/MV_XMSGARL/MV_XMSGCUP/MV_XMSGIST/MV_XMSGTER/MV_XVOPCMP/MV_XCONFCX/MV_XCONSME/" 
		cParamet += "MV_XNFACOB/MV_XNFCONT/MV_XNFRECU/MV_XNGDESC/MV_XNOMEMP/MV_XNROTEN/MV_XOBSFAT/MV_XPAGFIL/MV_XPAGPRE/MV_XPAGTIP/" 
		cParamet += "MV_XPDFTAB/MV_XPERCGP/MV_XPFXCOM/MV_XPOSTO /MV_XPRFXRS/MV_XRATAUT/MV_XRATFCP/MV_XRECFIL/MV_XRECPRE/MV_XRECTIP/" 
		cParamet += "MV_XREPSAE/MV_XSEE   /MV_XSERFAT/MV_XSRVPDV/MV_XTABPRO/MV_XTFPCLI/MV_XTMBREL/MV_XTMFREL/MV_XTMGANH/MV_XTMPBOL/" 
		cParamet += "MV_XTMPERD/MV_XTMPFAT/MV_XTPFTAU/MV_XTPLIVR/MV_XTPPROD/MV_XTPPROD/MV_XTXPER /MV_XTXRENE/MV_XUSITUA/MV_XVENCCF/" 
		cParamet += "MV_XVIASNP/MV_XVLDCXA/MV_XVLDDIV/TP_ACTBXTR/TP_ACTCF  /TP_ACTCHT /TP_ACTCMP /TP_ACTNP  /TP_ACTSQ  /TP_ACTVCR /" 
		cParamet += "TP_ACTVLH /TP_ACTVLS /TP_ACTDP  /MV_LJADFLD/MV_XADMFID/MV_XDTABAS/MV_XHRABAS/MV_XMNABAS/MV_LJORCAM/MV_XDIASPR/" 
		cParamet += "MV_LJORCAA/TP_ACTORC /MV_LJORPAR/MV_CXLOJA /MV_LJALTCX/MV_XCONDCF/MV_XUSRADM/ES_ALCDES /ES_ALCDPN /ES_ALCLIM /" 
		cParamet += "ES_ALCCMP /ES_ALCSAQ /ES_ALCDCX /ES_ALCDTIT/ES_ALCLOG /MV_XPE1300/MV_XMONMIN/MV_XVLDDEV/MV_XQTD1  /TP_SSVIASA/" 
		cParamet += "MV_XMAILFP/MV_XDTMAX /MV_XVERCTB/MV_XTIPSAC/MV_XMOTEXC/MV_XDECIMP/MV_XMRGVAL/MV_LJFISMS/TP_ACESSTF/TP_CCESEXP/" 
		cParamet += "MV_XCRCLIP/MV_XFUNBOL/MV_XTPCRC /ES_ALCLID /MV_VLDDTAB/MV_DTUVABA/ES_LOGCCX /MV_XGERENT/TP_ACTCONV/MV_XSERCA /" 
		cParamet += "MV_XPECRC /TP_MULTAFE/MV_XQTQLMC/MV_LJADMFI/MV_TELAFIN/MV_XBLQAI0/MV_LJORCAM/MV_LJORCAA/MV_XCARGVD/MV_XBICMOV/" 
		cParamet += "TP_RIDENTF/MV_LOJANF /MV_SERIE  /MV_FISNOTA/MV_LJTNINT/MV_LJIMPCR/ES_ALCTRC /MV_XAVINEX/MV_XPENFCE/MV_XURLTSS/"
		cParamet += "MV_XIMPDNF/MV_XINFVEN/MV_XSELADM/MV_XIDTSA3/TP_BLQCANC/TP_HCTRASQ/TP_DA4SOBR/TP_ZERAUF2/TP_IMPAGLC/TP_DETTVEN/"
		cParamet += "TP_IMPOBSC/TP_FCHABAS/TP_VLEMTCF/TP_IMPAFER/"

		// lista de parametros com valores defaults
		// aListPar -> [nome], [tipo], [conteúdo], [descrição] (150 caracteres)
		aadd(aListPar, { "MV_LJPOSTO", "L", ".T.", "Habilita Posto de Combustível no Totvs PDV." })
		aadd(aListPar, { "MV_XPOSTO ", "L", ".T.", "Habilita Posto de Combustível (VIRTUS)." })
		aadd(aListPar, { "MV_XSRVPDV", "L", ".T.", "Servidor PDV ou base Totvs PDV ? (default .T.)" })
		aadd(aListPar, { "ES_AJUS054", "L", ".T.", "Atualiza a tabela SPED054, quando ocorrer recuperação de venda ? (default .T.)" })
		aadd(aListPar, { "ES_ALCADA ", "L", ".F.", "Habilita controle de alcadas ? (default .F.)" })
		aadd(aListPar, { "ES_ALCDES ", "L", ".F.", "Habilita controle de alcadas de desconto? (default .F.)" })
		aadd(aListPar, { "ES_ALCDPN ", "L", ".F.", "Habilita controle de alcadas de desconto sobre preço negociado? (default .F.)" })
		aadd(aListPar, { "ES_ALCLIM ", "L", ".F.", "Habilita controle de alcadas de bloqueio e limite de credito? (default .F.)" })
		aadd(aListPar, { "ES_ALCCMP ", "L", ".F.", "Habilita controle de alcadas de valor maximo compensacao? (default .F.)" })
		aadd(aListPar, { "ES_ALCSAQ ", "L", ".F.", "Habilita controle de alcadas de saque pos pago? (default .F.)" })
		aadd(aListPar, { "ES_ALCDCX ", "L", ".F.", "Habilita controle de alcadas de diferenca de caixa? (default .F.)" })
		aadd(aListPar, { "ES_ALCDTIT", "L", ".F.", "Habilita controle de alcadas de desconto sobre titulos? (default .F.)" })
		aadd(aListPar, { "ES_ALCLOG ", "L", ".T.", "Habilita controle de log nas alcadas (default .T.)" })
		aadd(aListPar, { "ES_ALCLID ", "L", ".F.", "Habilita controle de saldo de alçada limite de desconto (default .F.)" })
		aadd(aListPar, { "ES_ALCTRC ", "L", ".F.", "Habilita controle de alçada de troco (default .F.)" })
		aadd(aListPar, { "ES_IPTSS  ", "C", GetServerIP(), "Ip Servidor TSS (default GetServerIP())" })
		aadd(aListPar, { "ES_DBTSS  ", "C", "postgres/sigatss", "DBMS/Base de Dados do TSS (default postgres/sigatss)" })
		aadd(aListPar, { "ES_PORTTSS", "N", "7890", "Porta da base de dados do TSS (default 7890)" })
		aadd(aListPar, { "ES_NWLTTSS", "C", "", "Nro lote para notas recuperadas via monitor PDV (parametro criado pelo sistema)" })
		aadd(aListPar, { "ESP_MOTOR ", "C", "", "Motorista padrao para entrada de devolucao de cupom fiscal - complemento fiscal CD6 (default 986981)" })
		aadd(aListPar, { "ESP_TRANSP", "C", "", "Transportadora padrao para entrada de devolucao de cupom fiscal - complemento fiscal CD6 (default 000001)" })
		aadd(aListPar, { "ESP_VEICUL", "C", "", "Veiculo padrao para entrada de devolucao de cupom fiscal - complemento fiscal CD6 (default 1491)" })
		aadd(aListPar, { "TP_ACTBXTR", "L", ".T.", "Habilita funcionalidade de baixa trocada no PDV (default .F.)" })
		aadd(aListPar, { "TP_ACTCHT ", "L", ".T.", "Habilita funcionalidade de cheque troco no PDV (default .F.)" })
		aadd(aListPar, { "TP_ACTCMP ", "L", ".T.", "Habilita funcionalidade de compensação no PDV (default .F.)" })
		aadd(aListPar, { "TP_ACTNP  ", "L", ".T.", "Habilita funcionalidade de nota a prazo no PDV (default .F.)" })
		aadd(aListPar, { "TP_ACTDP  ", "L", ".T.", "Habilita funcionalidade de depósito no PDV (default .F.)" })
		aadd(aListPar, { "TP_ACTSQ  ", "L", ".T.", "Habilita funcionalidade de saque no PDV (default .F.)" })
		aadd(aListPar, { "TP_ACTVCR ", "L", ".T.", "Habilita funcionalidade de validação de limite de crédito no PDV (default .F.)" })
		aadd(aListPar, { "TP_ACTVLH ", "L", ".T.", "Habilita funcionalidade de vale haver no PDV (default .F.)" })
		aadd(aListPar, { "TP_ACTVLS ", "L", ".T.", "Habilita funcionalidade de vale serviço no PDV (default .F.)" })
		aadd(aListPar, { "TP_ACTCF  ", "L", ".T.", "Habilita funcionalidade de carta frete no PDV (default .F.)" })
		aadd(aListPar, { "TP_ACTORC ", "L", ".T.", "Habilita funcionalidade de imprimir e gravar orçamento no host superior (default .F.)" })
		aadd(aListPar, { "TP_ACESSTF", "L", ".F.", "Habilita validacao acesso opcao troca forma, conferencia caixa (default .F.)" })
		aadd(aListPar, { "TP_EXITSYS", "L", ".T.", "Habilita fechar sistema ao sair da tela do PDV." })
		aadd(aListPar, { "MV_XADMFID", "C", "", "Adm. Financeiras para mudança de forma cartão para outros na emissão na nota. (Pegaponto)" })
		aadd(aListPar, { "MV_XDTABAS", "C", ""+DtoS(Date())+"", "Data do ultimo envio de e-mail com status de abastecimentos (erro/pendentes)" })
		aadd(aListPar, { "MV_XHRABAS", "C", ""+Time()+"", "Hora do ultimo envio de e-mail com status de abastecimentos (erro/pendentes)" })
		aadd(aListPar, { "MV_XMNABAS", "N", "120", "Invervalo minimo em minutos para envio da análise de status de abastecimentos" })
		aadd(aListPar, { "MV_XCONDCF", "C", "001", "Condição de Pagamento para emitente de carta frete." })
		aadd(aListPar, { "MV_XUSRADM", "C", "", "Define usuarios administradores para acessar rotina Controle de Acessos." })
		aadd(aListPar, { "MV_XPE1300", "C", "TRETP012", "Nome do ExecBlock a ser executado para geração da movimentação diária de combustíveis no bloco SPED 1300." })
		aadd(aListPar, { "MV_XMONMIN", "N", "3600", "JOb Monitor PDV: desconsidera os ultimos X segundos para não interferir no processo do PDV." })
		aadd(aListPar, { "MV_XVLDDEV", "L", ".T.", "Habilita validação de nota autorizada na devolução (default .F.)" })
		aadd(aListPar, { "MV_XBCOCDV", "C", "", "Banco de Devoluções: banco + agencia + conta (A6_COD+A6_AGENCIA+A6_NUMCON)" })
		aadd(aListPar, { "MV_LJFISMS", "C", "&U_TPDVE005()", "Mensagem padrao para impressao no rodape do cupom" })
		aadd(aListPar, { "MV_XPROCON", "C", "PROCON MT - Av. Baltazar Navarros, N.567, Bairro Bandeirantes, Cuiabá-MT, CEP 78010-020. Tel: 151 ou (65)3613-2100", "Mensagens informativas do cupom fiscal: Procon" })
		aadd(aListPar, { "TP_CCESEXP", "C", "", "Define o centro de custo para transferencia estoque exposição." })
		aadd(aListPar, { "MV_XCRCLIP", "L", ".T.", "Habilita bloqueio de CREDITO para cliente padrão (default .F.)" })
		aadd(aListPar, { "MV_XFUNBOL", "C", "TRETR009", "Funcao para definir fonte impressao boleto, rotinas faturamento. Default (TRETR009)" })
		aadd(aListPar, { "TP_PSWVEND", "L", ".T.", "Habilita controle de caixa por Vendedor, com exigência de senha." })
		aadd(aListPar, { "TP_CPSWVEN", "C", "A3_SENHA", "Define o campo senha a ser utilizado no cadastro do vendedor." })
		aadd(aListPar, { "MV_XTPCRC ", "L", ".T.", "Habilita o uso rotinas CRC." })
		aadd(aListPar, { "MV_VLDDTAB", "L", ".T.", "Habilita validacao data do abastecimento, dia anterior" })
		aadd(aListPar, { "MV_DTUVABA", "C", "", "Ultima data validacao do abastecimento, dia anterior" })
		aadd(aListPar, { "ES_LOGCCX ", "L", ".T.", "Habilita gravaçao log na rotina conferencia de caixa." })
		aadd(aListPar, { "ESP_XAGLUT", "N", "0.5", "Aglutinar abastecimentos: divergência máxima para considerar que sejam consecutivos" })
		aadd(aListPar, { "MV_XGERENT", "L", ".F.", "Habilita filtro combobox da sangria e suprimento pelo campo A6_XGERENT." })
		aadd(aListPar, { "MV_CXLOJA ", "C", "", "Códigos dos Caixas Gerais (BANCO/AGENCIA/CONTA), separados por /." })
		aadd(aListPar, { "TP_ACTCONV", "L", ".F.", "Habilita o uso de conveniência na central." })
		aadd(aListPar, { "MV_XSERCA ", "C", "A", "Define a série da catraca." })
		aadd(aListPar, { "MV_XPECRC ", "C", "2", "Qual PE usar para CRC: 1-Sigaloja;2=TotvsPDV. O valor Default é 2." })
		aadd(aListPar, { "TP_MULTAFE", "L", ".F.", "Habilita aferição selecionando vários itens (Multi-Seleção)." })
		aadd(aListPar, { "MV_XQTQLMC", "N", "20", "Define a quantidade de tanques a ser considerado nas rotinas LMC (criar campos LF1_ESTIxx e LF1_VTAQxx)" })
		aadd(aListPar, { "MV_XBLQAI0", "L", ".F.", "Habilita bloqueio de venda na filial, olhando para tabela AI0 (campo AI0_XBLFIL)." })
		aadd(aListPar, { "MV_LJORCAM", "C", "L1_XTICKET", "Campo de busca pincipal para buscar orçamento no host superior." })
		aadd(aListPar, { "MV_LJORCAA", "C", "L1_CGCMOTO", "Campo de busca secundário para buscar orçamento no host superior." })
		aadd(aListPar, { "MRPC_CAIXA", "C", "", "Determina o codigo de cada ambiente no PDV, preencher parametro por filial." })
		aadd(aListPar, { "MV_XCARGVD", "C", "", "Lista de cargos de vendedores habilitados para para o PDV." })
		aadd(aListPar, { "MV_XBICMOV", "L", ".T.", "Lista bicos nos registros SPED 1350 (BOMBAS), apenas quando possuir movimentos?  (default .T.)" })
		aadd(aListPar, { "MV_XFTCONV", "L", ".F.", "Define se rotina fatura ira trabalhar em modo conveniencia (default .F.)" })
		aadd(aListPar, { "MV_XPARCPG", "L", ".T.", "Define se a quantidade de parcelas cartao credito será definida pela condição de pagamento. (default .T.)" })
		aadd(aListPar, { "MV_XAVINEX", "L", ".T.", "Avalia inconsistencias extrato, rotina de Conciliação Cartão? (default .T.)" })
		aadd(aListPar, { "TP_RIDENTF", "C", "", "Modelo de relatório por identifid (vendedor): O-Old/N-New" })
		aadd(aListPar, { "TP_FPGCONV", "C", "", "Formas de pagamento adicionais, tratadas como convênios (igual NP)" })
		aadd(aListPar, { "MV_LJADFLD", "C", "A1_LOJA/A1_EST/A1_MUN", "Permite acrescentar campos na grid da pesquisa de clientes do Totvs PDV." })
		aadd(aListPar, { "MV_XFPGFAT", "C", "NP/BOL/RP/CF/FT/CC/CCP/CD/CDP/DP/CT/CTF/NF/VLS/RE/REN/CN", "Define as formas/tipos de pagamentos disponiveis para Faturamento." })
		aadd(aListPar, { "TP_SSVIASA", "N", "0", "Define o numero de vias adicionais para sangria e suprimento TotvsPDV." })
		aadd(aListPar, { "MV_XMOTEXF", "L", ".F.", "Habilita log de exclusão de faturas." })
		aadd(aListPar, { "MV_XX5MEXF", "C", "Z5", "Id da Tabela SX5 de motivos de exclusão de faturas." })
		aadd(aListPar, { "MV_XPENFCE", "L", ".F.", "Habilita pergunta para NF-e, quando não comunicar com TSS RETAGUARDA: Não será possível emitir NF-e, deseja emitir NFC-e? (ambiente com TSS OFF-LINE)" })
		aadd(aListPar, { "MV_XURLTSS", "C", "", "URL do TSS ON-LINE - Ex.: http://192.168.1.246:8092" })
		aadd(aListPar, { "MV_XNGDESC", "L", ".F.", "Ativa negociação pelo valor de desconto: U25_DESPBA" })
		aadd(aListPar, { "MV_XATUPRC", "L", ".T.", "Tipo de negociação no Totvs PDV: DESCONTO (.F.) ou PREÇO UNITÁRIO (.T.)"})
		aadd(aListPar, { "MV_XIMPDNF", "L", ".F.", "Habilita pergunta de impressão de danfinha (default .F.)" })
		aadd(aListPar, { "MV_XINFVEN", "L", ".T.", "Habilita dados do vendedor no rodapé do cupom (default .F.)" })
		aadd(aListPar, { "MV_XSELADM", "L", ".T.", "Define se ao inves de selecionar OPERADORA + BANDEIRA (.F.), será selecionado ADM. FINANCEIRA (.T.) (default .F.)" })
		aadd(aListPar, { "MV_XIDTSA3", "L", ".F.", "Define se vai gravar o campo A3_RFID automaticamente (default .T.)" })
		aadd(aListPar, { "TP_BLQCANC", "L", ".F.", "Bloqueia cancelamento de NFC-e quando ambiente estiver em contingência ou sem protocolo? (default .F.)" })
		aadd(aListPar, { "TP_HCTRASQ", "L", ".T.", "Habilita controle de transação na rotina de saque/vale ? (default .T.)" })
		aadd(aListPar, { "TP_HFA330Q", "L", ".T.", "Habilita a manipulação de query na FINA330 - compensação de valores (PE FA330QRY) ? (default .T.)" })
		aadd(aListPar, { "MV_XCOMBUS", "L", "", "Grupos de produtos, separados por '/', que identificam operações com combustíveis. (Somente combustiveis: GASOLINA, ETANOL e DIESEL)" })
		aadd(aListPar, { "TP_DA4SOBR", "N", 0, "Define se na importação de placas, quando placa ja vinculada a cliente/grupo se: 0 - Perguntar/1 - Sobrescrever/2 - Pular." })
		aadd(aListPar, { "MV_XPERCGP", "N", 0.6, "Define o percentual para perdas e sobras no LMC." })
		aadd(aListPar, { "TP_ZERAUF2", "L", ".T.", "Zera o valor do cheque troco quando processa estorno? (default .T.)" })
		aadd(aListPar, { "TP_IMPAGLC", "L", ".T.", "Imprime o aglutinado de cartão por cliente (default .T.)" })
		aadd(aListPar, { "TP_DETTVEN", "L", ".T.", "Detalhamento de troco em dinheiro das vendas (default .T.)" })
		aadd(aListPar, { "TP_IMPOBSC", "L", ".T.", "Imprime o observações da conferência de caixa (default .T.)" })
		aadd(aListPar, { "TP_FCHABAS", "L", ".F.", "No fechamento de caixa, valida se possui abastecimentos pendêntes (default .F.)" })
		aadd(aListPar, { "TP_VLEMTCF", "L", ".F.", "Valida equivalência: cliente vs emitente de carta frete (default .F.)" })
		aadd(aListPar, { "TP_IMPAFER", "L", ".T.", "Habilita/Desabilita impressão do comprovante de aferição (default .T.) " })

	Case cRot == "002" // Preços em Níveis

		// Lista dos parametros encontrados no projeto tpdvposto
		cParamet += "MV_XNIVCBC/MV_XTABNV0/MV_XTABNV1/MV_XTABNV2/MV_LJCNVDA/MV_TABPAD /MV_XPDFTAB/" // 7

		// lista de parametros com valores defaults
		// aListPar -> [nome], [tipo], [conteúdo], [descrição] (150 caracteres)
		aadd(aListPar, { "MV_XNIVCBC", "L", ".F.", "Ativar/desativar uso de preços em níveis na itegraçao CBC ? (default .F.)" })
		aadd(aListPar, { "MV_XTABNV0", "C", "001", "Definir a tabela de preços utilizada para preço nível 0 (zero), Dinheiro." })
		aadd(aListPar, { "MV_XTABNV1", "C", "001", "Definir a tabela de preços utilizada para preço nível 1 (um), Débito." })
		aadd(aListPar, { "MV_XTABNV2", "C", "001", "Definir a tabela de preços utilizada para preço nível 2 (dois), Crédito." })
		aadd(aListPar, { "MV_XPDFTAB", "L", ".F.", "Permite baixa de abastecimento cujo o preço de bomba é diferente do preço de tabela ? (default .F.)"})
	
	Case cRot == "003" // Naturezas Financeiras

		cParamet += "MV_NATCART/MV_NATCHEQ/MV_NATCONV/MV_NATCRED/MV_NATDINH/MV_NATFIN /MV_NATOUTR/MV_NATTEF /MV_NATTROC/MV_NATVALE"
		cParamet += "MV_XCNATCC/MV_XCNATCD/MV_XCNATCF/MV_XCNATCH/MV_XCNATCT/MV_XCNATDI/TP_NATNP  /TP_NATCF  /"
		cParamet += "MV_XNATCHA/MV_XNATFAL/MV_XNATFAT/MV_XCNATVL/"
		cParamet += "MV_XNATFUN/MV_XNATNCC/MV_XNATNDC/MV_XNATRA /MV_XNATRPS/MV_XNATTNC/MV_XNATVSO/MV_XNATVSP/MV_XNATVSR/MV_XNATVST/"

	End Case

	aParamet := StrTokArr2(cParamet, "/", .T.)
	For ni := 1 to Len(aParamet)
		If !Empty(aParamet[ni]) .and. aScan( aListPar, { |x| x[1] == aParamet[ni] } ) <= 0
			aadd(aListPar, {aParamet[ni],"","",""})
		EndIf
	Next ni

	For ni := 1 to len(aListPar)
		If Eval(bGetMvFil,aListPar[ni][1],.T.,)
			aAdd(aVetPar,{ "overd", aListPar[ni][1], (cAliasSX6)->&("X6_TIPO"), Alltrim(X6Conteud()), Alltrim(X6Descric())+" "+Alltrim(X6Desc1())+" "+Alltrim(X6Desc2()) })
		Else
			aAdd(aVetPar,{ "overm", aListPar[ni][1], aListPar[ni][2], aListPar[ni][3], Alltrim(aListPar[ni][4]) })
		EndIf
	Next ni

	If len(aVetPar) <= 0
		aAdd(aVetPar,{"overm","","","",""})
	EndIf
	Asort(aVetPar,,,{|x,y| x[2] < y[2] }) //ordem crescente

	If lRefr
		oLbPar:nAt := 1
		oLbPar:SetArray(aVetPar)
		oLbPar:bLine := { || { &(aVetPar[oLbPar:nAt,1]) , aVetPar[oLbPar:nAt,2] , aVetPar[oLbPar:nAt,3] , 	aVetPar[oLbPar:nAt,4] , aVetPar[oLbPar:nAt,5] }}
		oLbPar:Refresh()
	EndIf

Return()







/*/{Protheus.doc} EscEmpresa
Funcao Generica para escolha de Empresa, montado pelo SM0.
Retorna vetor contendo as selecoes feitas.
Se nao For marcada nenhuma o vetor volta vazio.

@author Totvs TBC
@since 31/10/2017

@version 1.0
@return ${return}, ${return_description}

@type function
/*/
User Function UPDESEMP(lShared)
Return EscEmpresa(lShared)
Static Function EscEmpresa(lShared)
//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Parametro  nTipo                           ³
//³ 1  - Monta com Todas Empresas/Filiais      ³
//³ 2  - Monta so com Empresas                 ³
//³ 3  - Monta so com Filiais de uma Empresa   ³
//³                                            ³
//³ Parametro  aMarcadas                       ³
//³ Vetor com Empresas/Filiais pre marcadas    ³
//³                                            ³
//³ Parametro  cEmpSel                         ³
//³ Empresa que sera usada para montar selecao ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	Local   aSalvAmb := GetArea()
	Local   aSalvSM0 := {}
	Local   aRet     := {}
	Local   aVetor   := {}
	Local   oDlg     := NIL
	Local   oChkMar  := NIL
	Local   oLbx     := NIL
	Local   oMascEmp := NIL
	Local   oMascFil := NIL
	Local   oButMarc := NIL
	Local   oButDMar := NIL
	Local   oButInv  := NIL
	Local   oSay     := NIL
	Local   oOk      := LoadBitmap( GetResources(), "LBOK" )
	Local   oNo      := LoadBitmap( GetResources(), "LBNO" )
	Local   lChk     := .F.
	Local   lOk      := .F.
	Local   lTeveMarc:= .F.
	Local   cVar     := ""
	Local   cNomEmp  := ""
	Local   cMascEmp := "??"
	Local   cMascFil := "??"
	Local   aMarcadas:= {}

	Default lShared	:= .F. //Caso verdadeiro, indica que a tabela deve ser aberta em modo compartilhado, isto é, outros processos também poderão abrir esta tabela.

	If !MyOpenSm0(lShared)
		Return aRet
	EndIf

	dbSelectArea( "SM0" )
	aSalvSM0 := SM0->( GetArea() )
	SM0->( dbSetOrder( 1 )) //M0_CODIGO+M0_CODFIL
	SM0->( dbGoTop() )

	While !SM0->( EOF() )

		If aScan( aVetor, {|x| x[2] == SM0->M0_CODIGO} ) == 0
			aAdd(  aVetor, { aScan( aMarcadas, {|x| x[1] == SM0->M0_CODIGO .and. x[2] == SM0->M0_CODFIL} ) > 0, SM0->M0_CODIGO, SM0->M0_CODFIL, SM0->M0_NOME, SM0->M0_FILIAL } )
		EndIf

		dbSkip()
	EndDo

	RestArea( aSalvSM0 )

	Define MSDialog  oDlg Title "" From 0, 0 To 270, 396 Pixel

	oDlg:cToolTip := "Tela para Múltiplas Seleções de Empresas/Filiais"

	oDlg:cTitle   := "Selecione a(s) Empresa(s) para Atualização"

	@ 10, 10 Listbox  oLbx Var  cVar Fields Header " ", " ", "Empresa" Size 178, 095 Of oDlg Pixel
	oLbx:SetArray(  aVetor )
	oLbx:bLine := {|| {IIf( aVetor[oLbx:nAt, 1], oOk, oNo ), ;
		aVetor[oLbx:nAt, 2], ;
		aVetor[oLbx:nAt, 4]}}
	oLbx:BlDblClick := { || aVetor[oLbx:nAt, 1] := !aVetor[oLbx:nAt, 1], VerTodos( aVetor, @lChk, oChkMar ), oChkMar:Refresh(), oLbx:Refresh()}
	oLbx:cToolTip   :=  oDlg:cTitle
	oLbx:lHScroll   := .F. // NoScroll

	@ 112, 10 CheckBox oChkMar Var  lChk Prompt "Todos"   Message  Size 40, 007 Pixel Of oDlg;
		on Click MarcaTodos( lChk, @aVetor, oLbx )

	@ 123, 10 Button oButInv Prompt "&Inverter"  Size 32, 12 Pixel Action ( InvSelecao( @aVetor, oLbx ), VerTodos( aVetor, @lChk, oChkMar ) ) ;
		Message "Inverter Seleção" Of oDlg

// Marca/Desmarca por mascara
	@ 113, 51 Say  oSay Prompt "Empresa" Size  40, 08 Of oDlg Pixel
	@ 112, 80 MSGet  oMascEmp Var  cMascEmp Size  05, 05 Pixel Picture "@!"  Valid (  cMascEmp := StrTran( cMascEmp, " ", "?" ), cMascFil := StrTran( cMascFil, " ", "?" ), oMascEmp:Refresh(), .T. ) ;
		Message "Máscara Empresa ( ?? )"  Of oDlg
	@ 123, 50 Button oButMarc Prompt "&Marcar"    Size 32, 12 Pixel Action ( MarcaMas( oLbx, aVetor, cMascEmp, .T. ), VerTodos( aVetor, @lChk, oChkMar ) ) ;
		Message "Marcar usando máscara ( ?? )"    Of oDlg
	@ 123, 80 Button oButDMar Prompt "&Desmarcar" Size 32, 12 Pixel Action ( MarcaMas( oLbx, aVetor, cMascEmp, .F. ), VerTodos( aVetor, @lChk, oChkMar ) ) ;
		Message "Desmarcar usando máscara ( ?? )" Of oDlg

	Define SButton From 111, 125 Type 1 Action ( RetSelecao( @aRet, aVetor ), oDlg:End() ) OnStop "Confirma a Seleção"  Enable Of oDlg
	Define SButton From 111, 158 Type 2 Action ( IIf( lTeveMarc, aRet :=  aMarcadas, .T. ), oDlg:End() ) OnStop "Abandona a Seleção" Enable Of oDlg
	Activate MSDialog  oDlg Center

	RestArea( aSalvAmb )
	dbSelectArea( "SM0" )
	dbCloseArea()

Return  aRet


/*/{Protheus.doc} MarcaTodos
Funcao Auxiliar para marcar/desmarcar todos os itens do ListBox ativo.

@author Ernani Forastieri
@since 31/10/2017
@version 1.0
@return ${return}, ${return_description}
@param lMarca, logical, descricao
@param aVetor, array, descricao
@param oLbx, object, descricao
@type function
/*/
Static Function MarcaTodos( lMarca, aVetor, oLbx )
	Local  nI := 0

	For nI := 1 To Len( aVetor )
		aVetor[nI][1] := lMarca
	Next nI

	oLbx:Refresh()

Return NIL


/*/{Protheus.doc} InvSelecao
Funcao Auxiliar para inverter selecao do ListBox Ativo.

@author Ernani Forastieri
@since 31/10/2017
@version 1.0

@return ${return}, ${return_description}
@param aVetor, array, descricao
@param oLbx, object, descricao

@type function
/*/
Static Function InvSelecao( aVetor, oLbx )
	Local  nI := 0

	For nI := 1 To Len( aVetor )
		aVetor[nI][1] := !aVetor[nI][1]
	Next nI

	oLbx:Refresh()

Return NIL


/*/{Protheus.doc} RetSelecao
Funcao Auxiliar que monta o retorno com as selecoes.

@author Ernani Forastieri
@since 31/10/2017
@version 1.0

@return ${return}, ${return_description}
@param aRet, array, descricao
@param aVetor, array, descricao

@type function
/*/
Static Function RetSelecao( aRet, aVetor )
	Local  nI    := 0

	aRet := {}
	For nI := 1 To Len( aVetor )
		If aVetor[nI][1]
			aAdd( aRet, { aVetor[nI][2] , aVetor[nI][3], aVetor[nI][2] +  aVetor[nI][3] } )
		EndIf
	Next nI

Return NIL


/*/{Protheus.doc} MarcaMas
Funcao para marcar/desmarcar usando mascaras.

@author Ernani Forastieri
@since 31/10/2017
@version 1.0

@return ${return}, ${return_description}

@param oLbx, object, descricao
@param aVetor, array, descricao
@param cMascEmp, characters, descricao
@param lMarDes, logical, descricao

@type function
/*/
Static Function MarcaMas( oLbx, aVetor, cMascEmp, lMarDes )
	Local cPos1 := SubStr( cMascEmp, 1, 1 )
	Local cPos2 := SubStr( cMascEmp, 2, 1 )
	Local nPos  := oLbx:nAt
	Local nZ    := 0

	For nZ := 1 To Len( aVetor )
		If cPos1 == "?" .or. SubStr( aVetor[nZ][2], 1, 1 ) == cPos1
			If cPos2 == "?" .or. SubStr( aVetor[nZ][2], 2, 1 ) == cPos2
				aVetor[nZ][1] :=  lMarDes
			EndIf
		EndIf
	Next

	oLbx:nAt := nPos
	oLbx:Refresh()

Return NIL


/*/{Protheus.doc} VerTodos
Funcao auxiliar para verificar se estao todos marcardos ou nao.

@author Ernani Forastieri
@since 31/10/2017
@version 1.0

@return ${return}, ${return_description}

@param aVetor, array, descricao
@param lChk, logical, descricao
@param oChkMar, object, descricao

@type function
/*/
Static Function VerTodos( aVetor, lChk, oChkMar )
	Local lTTrue := .T.
	Local nI     := 0

	For nI := 1 To Len( aVetor )
		lTTrue := IIf( !aVetor[nI][1], .F., lTTrue )
	Next nI

	lChk := IIf( lTTrue, .T., .F. )
	oChkMar:Refresh()

Return NIL


/*/{Protheus.doc} MyOpenSM0
Funcao de processamento abertura do SM0 modo exclusivo.

@author Totvs TBC
@since 19/08/2015
@version 1.0

@return ${return}, ${return_description}
@param lShared, logical, Caso verdadeiro, indica que a tabela deve ser aberta em modo compartilhado, isto é, outros processos também poderão abrir esta tabela.

@type function
/*/
User Function UPDOPSM0(lShared)
Return MyOpenSM0(lShared)
Static Function MyOpenSM0(lShared)

	Local lOpen := .F.
	Local nLoop := 0

	For nLoop := 1 To 20 //faz 20 tentativas

		If lShared
			OpenSm0() //Essa função realiza a abertura do SIGAMAT, utilizando como alias o SM0.
		Else
			OpenSM0Excl() //Essa função realiza a abertura do SIGAMAT em modo EXCLUSIVO, utilizando como alias o SM0.
		EndIf

		If !Empty( Select( "SM0" ) )
			lOpen := .T.
			Exit
		EndIf

		Sleep( 500 )

	Next nLoop

	If !lOpen
		Help(NIL, NIL, "ATENÇÃO", NIL, "Não foi possível a abertura da tabela " + ;
			IIf( lShared, "de empresas (SM0).", "de empresas (SM0) de forma exclusiva." ), 1, 0, NIL, NIL, NIL, NIL, NIL, {""})
	EndIf

Return lOpen

